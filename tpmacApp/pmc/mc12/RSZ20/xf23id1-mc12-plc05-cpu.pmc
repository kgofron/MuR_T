;####################################################
; Calculation of CPU load for a Turbo PMAC
;
; Ronaldo Mercado 2009-04-14
; Mostly copied from "Turbo SRM.pdf" [1] 
; Adjusted P variables to be in the P5xx range as per 
; "PMAC Rules and Guidelines" [2]
; Modified original formula for end result P575, 
; to reflect a first order filter with alpha=1/16 (1/P576)
;
; [1] http://www.deltatau.com/fmenu/TURBO%20SRM.PDF page 17 (35 of 795)
; [2] http://www.cs.diamond.ac.uk/cgi-bin/wiki.cgi/PMAC_PLC_Rules_and_Guidelines 
;
; Macros:
; - PLC: plc number
;
; Result stored in
;  P575
; Uses variables 
;  P567..576
;####################################################

P570=4 ; 4 phase interrupts per servo interrupt
P576=16 ; Length of filter for averaging duty cycle

OPEN PLC 5 
CLEAR

    P571=M71/M70 ; Phase task duty cycle
    P569=INT(M72/M70) ; # of times phase interrupted servo
    P572=(M72-P569*M71)/(M70*P570) ; Servo task duty cycle
    P568=INT(M73/M70) ; # of times phase interrupted RTI
    P567=INT(M73/(M70*P570)) ; # of times servo interrupted RTI
    P573=(M73-P568*M71-P567*(M72-P569*M71))/(M70*P570*(I8+1))

    ; RTI task duty cycle
    P574=P571+P572+P573 ; Latest total foreground duty cycle
    P575=(P574+(P576-1)*P575)/P576 ; Averaged total foreground duty cycle

    ; Also use this PLC to report all PLC, GPIO and motion program status
    P580 = 32768
    P581 = 0
    P582 = 0 ; PLC 0-15
    P583 = 0 ; PLC 16-31
    P584 = 0 ; GPIO inputs
    P585 = 0 ; GPIO outputs
    P586 = 0 ; Motion programs

    WHILE (P581 < 16)
      ; PLCS 0-15
      P582=P582 + P580 * (-1 * M(5000+P581) + 1)
      ; PLCS 16-31
      P583=P583 + P580 * (-1 * M(5016+P581) + 1)
      ; GPIO inputs
      P584=P584 + P580 * M(0+P581)
      ; GPIO outputs
      IF (P581 < 8)
        P585 = P585 + P580 * M(32+P581)
      ENDIF
      ; Motion programs
      P586 = P586 + P580 * M(5180+100*P581)

      ; Update counters
      P580 = P580 / 2
      P581 = P581 + 1
    ENDW

    ; Take snapshot of PLC status for reporting to EPICS
    P592 = P582 ; PLC 0-15
    P593 = P583 ; PLC 16-31
    P594 = P584 ; GPIO inputs
    P595 = P585 ; GPIO outputs
    P596 = P586 ; Motion programs

CLOSE

ENABLE PLC 5
